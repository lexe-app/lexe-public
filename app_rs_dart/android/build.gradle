// The Android Gradle Plugin builds the native code with the Android NDK.

group = "app.lexe.app_rs_dart"
version = "1.0"

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // The Android Gradle Plugin knows how to build native code with the NDK.
        classpath("com.android.tools.build:gradle:8.13.2")
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"

android {
    namespace = "app.lexe.app_rs_dart"

    // println "app_rs_dart: flutter.compileSdkVersion: ${flutter.compileSdkVersion}"
    // println "app_rs_dart: android.ndkVersion: ${android.ndkVersion}"

    // Bumping the plugin compileSdk version requires all clients of this plugin
    // to bump the version in their app.
    compileSdk = 35

    // Use the NDK version declared in /android/app/build.gradle file of the
    // Flutter project. Replace it with a version number if this plugin requires
    // a specific NDK version. (e.g. ndkVersion "23.1.7779620")
    //
    // Match the value in `app/android/app/build.gradle.kts`.
    ndkVersion = "28.2.13676358"
    // ndkVersion = android.ndkVersion


    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    defaultConfig {
        minSdk = 23
    }
}

//
// Custom gradle build hook
//
// Runs `cargo ndk build -p app-rs` to get the native shared library and tells
// gradle to include it in the built package.
//

// Dump gradle values
//
// [`android`](https://cs.android.com/android-studio/platform/tools/base/+/mirror-goog-studio-main:build-system/gradle-core/src/main/java/com/android/build/gradle/BaseExtension.kt)
//
// // ex: "android-34"
// println "app_rs_dart: android.compileSdkVersion: ${project.android.compileSdkVersion}"
// // ex: 21
// println "app_rs_dart: android.minSdkVersion.apiLevel: ${project.android.defaultConfig.minSdkVersion.apiLevel}"
// // ex: "/Users/phlip9/.local/android"
// println "app_rs_dart: android.sdkDirectory: '${project.android.sdkDirectory}'"
// // ex: "/Users/phlip9/.local/android/ndk/23.1.7779620"
// println "app_rs_dart: android.ndkDirectory: '${project.android.ndkDirectory}'"
// // ex: "/Users/phlip9/dev/lexe/public/app/build/app_rs_dart"
// println "app_rs_dart: project.buildDir: '${project.buildDir}'"

// Collect the native `rustc` target triples that flutter wants to build as a
// space-separated string.
//
// ex: -> "aarch64-linux-android"
// ex: -> "aarch64-linux-android armv7-linux-androideabi x86_64-linux-android"
def getRustTargets() {
    // BTreeSet<String>
    def out = new TreeSet<String>();

    // flutter target platform -> rustc target triples
    def platformToRust = [
        "android-arm": "armv7-linux-androideabi",
        "android-arm64": "aarch64-linux-android",
        "android-x64": "x86_64-linux-android"
    ]

    // Search for `FlutterPlugin` and add `getTargetPlatforms()` as rustc target
    // triples.
    for (project in rootProject.childProjects) {
        // Find the `FlutterPlugin`
        def flutterPlugin = null
        for (plugin in project.value.getPlugins()) {
            if (plugin.class.name == "FlutterPlugin") {
                flutterPlugin = plugin;
                break;
            }
        }
        if (!flutterPlugin) {
            continue
        }

        // Add the rust target triples
        flutterPlugin.getTargetPlatforms().collect().each { platform ->
            def rustTarget = platformToRust[platform]
            if (rustTarget == null) {
                throw new GradleException("app_rs_dart: unrecognized flutter target platform: ${platform}")
            }
            out.add(rustTarget)
        }
    }

    // Return as a space-separated string, since we'll be passing via env var.
    return out.join(" ")
}

// project: [`Project`](https://docs.gradle.org/current/kotlin-dsl/gradle/org.gradle.api/-project/index.html)
project.android.buildTypes.all { buildType ->
    def profileName = buildType.name.capitalize()
    def profileArg = buildType.isDebuggable() ? null : '--release'
    def hookTaskName = "merge${profileName}NativeLibs"

    def outDir = "${project.buildDir}/jniLibs/${buildType.name}"
    def jniLibs = project.android.sourceSets.maybeCreate(buildType.name).jniLibs;
    jniLibs.srcDir(new File(outDir))

    // println "app_rs_dart: android.buildTypes -> $profileName"

    // Register a new task that runs `cargo ndk build` with the right debug vs
    // release build profile. We don't currently build the native Rust lib
    // differently for different `productFlavors`.
    // 
    // tasks: [`TaskCollection`](https://docs.gradle.org/current/kotlin-dsl/gradle/org.gradle.api.tasks/-task-collection/index.html)
    // [`Exec`](https://docs.gradle.org/current/kotlin-dsl/gradle/org.gradle.api.tasks/-exec/index.html)
    def cargoBuildTask = project.tasks.register("cargoBuild$profileName", Exec) {
        workingDir = ".."
        commandLine "./build_rust_android.sh"

        if (profileArg != null) {
            args profileArg
        }

        // Set some environment variables
        // `cargo ndk` will use this to pick the right Android NDK toolchain
        environment ANDROID_NDK_HOME: project.android.ndkDirectory
        // Where we're supposed to dump the `libapp_rs.so` shared libs.
        environment APP_RS__OUT_DIR: outDir
        // "android-34" -> "34"
        environment APP_RS__COMPILE_SDK_VERSION: project.android.compileSdkVersion.substring(8)
        // The space-separated rustc targets that flutter wants to build.
        // ex: -> "aarch64-linux-android"
        // ex: -> "aarch64-linux-android armv7-linux-androideabi x86_64-linux-android"
        environment APP_RS__TARGETS: getRustTargets()
    }

    // println "app_rs_dart: registered: $cargoBuildTask"

    // Run cargo build (with the right profile) before of these tasks:
    //   `mergeDebugNativeLibs`
    //   `mergeProfileNativeLibs`
    //   `mergeReleaseNativeLibs`
    //
    // task: [`Task`](https://docs.gradle.org/current/kotlin-dsl/gradle/org.gradle.api/-task/depends-on.html)
    project.tasks.whenTaskAdded { task ->
        if (task.name == hookTaskName) {
            // println "app_rs_dart: $task --dependsOn-> $cargoBuildTask"
            task.dependsOn cargoBuildTask
        }
    }
}
